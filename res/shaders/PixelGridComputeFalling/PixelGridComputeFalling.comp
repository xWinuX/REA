#version 450

#include "../PixelGridComputeGlobals.glsl"

// Pixel with {PixelID: 255, Flags: [Solid], Density: minValue, Spread: 0}
//const Pixel solidPixel = { 0x000001FF };
//const Pixel solidPixel = { 0xFF01F000 };

layout (push_constant) uniform PushConstant {
    uint readIndex;
    uint writeIndex;
    uint flowIteration;
    uvec2 margolusOffset;
};

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;
void main()
{
    uint simulationWidth = simulationData.simulationWidth;
    uint simulationHeight = simulationData.simulationHeight;

    if (gl_GlobalInvocationID.x >= (simulationWidth * simulationHeight)) {
        return;
    }

    uint cellStep = gl_GlobalInvocationID.x * 2;
    uint margolusX = ((cellStep + margolusOffset.x) % simulationWidth);
    uint margolusY = (((cellStep / simulationWidth) * 2) + 1) - margolusOffset.y;

    uint x = margolusX;
    uint y = margolusY;

    uint topRightX = (x + 1) % simulationWidth;
    uint topRightY = y;

    uint bottomLeftX = x;
    uint bottomLeftY = (y == 0) ? simulationHeight - 1 : y - 1;

    uint bottomRightX = topRightX;
    uint bottomRightY = bottomLeftY;

    // Calculate wrapped indices
    uvec2 readOffset = uvec2(0, 0);
    uvec2 writeOffset = uvec2(0, 0);

    uint readWidth = simulationWidth;
    uint writeWidth = simulationWidth;

    if (readIndex == 0) {
        readOffset = uvec2(simulationData.targetPosition);
        readWidth = simulationData.width;
    }

    if (writeIndex == 0) {
        writeOffset = uvec2(simulationData.targetPosition);
        writeWidth = simulationData.width;
    }

    uint topLeftIndexRead = getGlobalIndex(x, y, readWidth, readOffset) + readIndex;
    uint topRightIndexRead = getGlobalIndex(topRightX, topRightY, readWidth, readOffset) + readIndex;
    uint bottomLeftIndexRead = getGlobalIndex(bottomLeftX, bottomLeftY, readWidth, readOffset) + readIndex;
    uint bottomRightIndexRead = getGlobalIndex(bottomRightX, bottomRightY, readWidth, readOffset) + readIndex;

    uint topLeftIndexWrite = getGlobalIndex(x, y, writeWidth, writeOffset) + writeIndex;
    uint topRightIndexWrite = getGlobalIndex(topRightX, topRightY, writeWidth, writeOffset) + writeIndex;
    uint bottomLeftIndexWrite = getGlobalIndex(bottomLeftX, bottomLeftY, writeWidth, writeOffset) + writeIndex;
    uint bottomRightIndexWrite = getGlobalIndex(bottomRightX, bottomRightY, writeWidth, writeOffset) + writeIndex;

    // Read pixels
    Pixel topLeftPixel = pixels[topLeftIndexRead];
    Pixel bottomLeftPixel = pixels[bottomLeftIndexRead];
    Pixel topRightPixel = pixels[topRightIndexRead];
    Pixel bottomRightPixel = pixels[bottomRightIndexRead];

    PixelData topLeftPixelData = simulationData.pixelLookup[topLeftPixel.PixelID16_Charge8_Flags8 & 0xFFFFu];
    PixelData bottomLeftPixelData = simulationData.pixelLookup[bottomLeftPixel.PixelID16_Charge8_Flags8 & 0xFFFFu];
    PixelData topRightPixelData = simulationData.pixelLookup[topRightPixel.PixelID16_Charge8_Flags8 & 0xFFFFu];
    PixelData bottomRightPixelData = simulationData.pixelLookup[bottomRightPixel.PixelID16_Charge8_Flags8 & 0xFFFFu];

    // Write pixels
    pixels[topLeftIndexWrite] = topLeftPixel;
    pixels[bottomLeftIndexWrite] = bottomLeftPixel;
    pixels[topRightIndexWrite] = topRightPixel;
    pixels[bottomRightIndexWrite] = bottomRightPixel;

    float topLeftTemperature = topLeftPixel.Temperature;
    float topRightTemperature = topRightPixel.Temperature;
    float bottomLeftTemperature = bottomLeftPixel.Temperature;
    float bottomRightTemperature = bottomRightPixel.Temperature;

    bool topLeftPixelSolid = bitsetHas(topLeftPixelData.Flags, Solid);
    bool bottomLeftPixelSolid = bitsetHas(bottomLeftPixelData.Flags, Solid);
    bool topRightPixelSolid = bitsetHas(topRightPixelData.Flags, Solid);
    bool bottomRightPixelSolid = bitsetHas(bottomRightPixelData.Flags, Solid);

    bool topLeftGravity = bitsetHas(topLeftPixelData.Flags, Gravity);
    bool topRightGravity = bitsetHas(topRightPixelData.Flags, Gravity);
    bool bottomLeftGravity = bitsetHas(bottomLeftPixelData.Flags, Gravity);
    bool bottomRightGravity = bitsetHas(bottomRightPixelData.Flags, Gravity);

    bool topLeftCanFlow = topLeftPixelData.Spread > 0;
    bool topRightCanFlow = topRightPixelData.Spread > 0;
    bool bottomLeftCanFlow = bottomLeftPixelData.Spread > 0;
    bool bottomRightCanFlow = bottomRightPixelData.Spread > 0;

    float step = 0.00002f;

    float topLeftDensity = topLeftPixelData.Density - (step * topLeftTemperature * float(topLeftCanFlow));
    float topRightDensity = topRightPixelData.Density - (step * topRightTemperature * float(topRightCanFlow));
    float bottomLeftDensity = bottomLeftPixelData.Density - (step * bottomLeftTemperature * float(bottomLeftCanFlow));
    float bottomRightDensity = bottomRightPixelData.Density - (step * bottomRightTemperature * float(bottomRightCanFlow));

    if (flowIteration == 0)
    {
        bool leftRowSwapped = false;
        bool rightRowSwapped = false;

        if (topLeftGravity && bottomLeftGravity && (topLeftCanFlow || !bottomLeftPixelSolid))
        {
            if (topLeftDensity > bottomLeftDensity)
            {
                pixels[topLeftIndexWrite] = bottomLeftPixel;
                pixels[bottomLeftIndexWrite] = topLeftPixel;

                Pixel tmpPixel = topLeftPixel;
                PixelData tmpPixelData = topLeftPixelData;
                float tmpPixelDensity = topLeftDensity;
                bool tmpGravity = topLeftGravity;

                topLeftPixel = bottomLeftPixel;
                topLeftPixelData = bottomLeftPixelData;
                topLeftDensity = bottomLeftDensity;
                topLeftGravity = bottomLeftGravity;

                bottomLeftPixel = tmpPixel;
                bottomLeftPixelData = tmpPixelData;
                bottomLeftDensity = tmpPixelDensity;
                bottomLeftGravity = tmpGravity;

                leftRowSwapped = true;
            }
        }

        if (topRightGravity && bottomRightGravity && (topRightCanFlow || !bottomRightPixelSolid))
        {
            if (topRightDensity > bottomRightDensity)
            {
                pixels[topRightIndexWrite] = bottomRightPixel;
                pixels[bottomRightIndexWrite] = topRightPixel;

                Pixel tmpPixel = topRightPixel;
                PixelData tmpPixelData = topRightPixelData;
                float tmpPixelDensity = topRightDensity;

                topRightPixel = bottomRightPixel;
                topRightPixelData = bottomRightPixelData;
                topRightDensity = bottomRightDensity;

                bottomRightPixel = tmpPixel;
                bottomRightPixelData = tmpPixelData;
                bottomRightDensity = tmpPixelDensity;

                rightRowSwapped = true;
            }
        }

        if ((topLeftPixelSolid && bottomRightPixelSolid) || (topRightPixelSolid && bottomLeftPixelSolid)) { return; }

        if (!leftRowSwapped && !rightRowSwapped)
        {
            if (topLeftGravity && bottomRightGravity && (topLeftCanFlow || !bottomRightPixelSolid) && topLeftDensity > bottomRightDensity)
            {
                pixels[topLeftIndexWrite] = bottomRightPixel;
                pixels[bottomRightIndexWrite] = topLeftPixel;

                Pixel tmpPixel = topLeftPixel;
                PixelData tmpPixelData = topLeftPixelData;
                float tmpPixelDensity = topLeftDensity;

                topLeftPixel = bottomRightPixel;
                topLeftPixelData = bottomRightPixelData;
                topLeftDensity = bottomRightDensity;

                bottomRightPixel = tmpPixel;
                bottomRightPixelData = tmpPixelData;
                bottomRightDensity = tmpPixelDensity;
            }
            else if (topRightGravity && bottomLeftGravity && (topRightCanFlow || !bottomLeftPixelSolid) && topRightDensity > bottomLeftDensity)
            {
                pixels[topRightIndexWrite] = bottomLeftPixel;
                pixels[bottomLeftIndexWrite] = topRightPixel;

                Pixel tmpPixel = topRightPixel;
                PixelData tmpPixelData = topRightPixelData;
                float tmpPixelDensity = topRightDensity;


                topRightPixel = bottomLeftPixel;
                topRightPixelData = bottomLeftPixelData;
                topRightDensity = bottomLeftDensity;

                bottomLeftPixel = tmpPixel;
                bottomLeftPixelData = tmpPixelData;
                bottomLeftDensity = tmpPixelDensity;
            }
        }
    }

    topLeftPixelSolid = bitsetHas(topLeftPixelData.Flags, Solid);
    bottomLeftPixelSolid = bitsetHas(bottomLeftPixelData.Flags, Solid);
    topRightPixelSolid = bitsetHas(topRightPixelData.Flags, Solid);
    bottomRightPixelSolid = bitsetHas(bottomRightPixelData.Flags, Solid);

    bool topLeftCanSpread = topLeftPixelData.Spread > flowIteration;
    bool topRightCanSpread = topRightPixelData.Spread > flowIteration;
    bool bottomLeftCanSpread = bottomLeftPixelData.Spread > flowIteration;
    bool bottomRightCanSpread = bottomRightPixelData.Spread > flowIteration;

    if (!topLeftCanSpread && !topRightCanSpread && !bottomLeftCanSpread && !bottomRightCanSpread) { return; }

    //if (!topLeftPixelSolid && !topRightPixelSolid)
    //{
    if (
    (topLeftGravity && topRightGravity && (bottomLeftPixelSolid || bottomLeftDensity >= topLeftDensity) && (bottomRightPixelSolid || bottomRightDensity >= topRightDensity)) && (
    (simulationData.rng >= 0.5f && topLeftCanSpread && topLeftDensity > topRightDensity) ||
    (simulationData.rng < 0.5f && topRightCanSpread && topRightDensity > topLeftDensity)
    )
    )
    {
        pixels[topLeftIndexWrite] = topRightPixel;
        pixels[topRightIndexWrite] = topLeftPixel;

        bool tmpSolid = topLeftPixelSolid;
        float tmpDensity = topLeftDensity;

        topLeftPixelSolid = topRightPixelSolid;
        topLeftDensity = topRightDensity;

        topRightPixelSolid = tmpSolid;
        topRightDensity = tmpDensity;
    }
    //}

    //if (!bottomLeftPixelSolid && !bottomRightPixelSolid)
    //{
    if (
    (bottomLeftGravity && bottomRightGravity && (topLeftPixelSolid || topLeftDensity >= bottomLeftDensity) && (topRightPixelSolid || topRightDensity >= bottomRightDensity)) && (
    (simulationData.rng >= 0.5f && bottomLeftCanSpread && bottomLeftDensity > bottomRightDensity) ||
    (simulationData.rng < 0.5f && bottomRightCanSpread && bottomRightDensity > bottomLeftDensity)
    )
    )
    {
        pixels[bottomLeftIndexWrite] = bottomRightPixel;
        pixels[bottomRightIndexWrite] = bottomLeftPixel;
    }
    //}
}
